import net.davidcie.gyroscopebandaid.ConstGenerator
apply plugin: 'com.android.application'

def generatedSrcDir = new File(buildDir, "generated/tld/src/main/java/")
def prefsXmlFile = new File(projectDir, "src/main/res/values/prefs.xml")

task generateConst << {
    ConstGenerator.generateConstClass(generatedSrcDir, prefsXmlFile)
}

preBuild.dependsOn generateConst

android {
    // Add the generated class to build sources
    sourceSets {
        main {
            java {
                srcDirs += generatedSrcDir
            }
        }
    }

    // Compile with the latest SDK to allow new compilation checks
    // and get early warning of newly deprecated APIs.
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "net.davidcie.gyroscopebandaid"
        // Only allow Lollipop (5.0) and above as
        // the latest Xposed API for ROMs before that
        // is v53 while we make use of APIs in v82.
        // Additionally, Material Design was also intoed
        // in API level 21.
        minSdkVersion 21
        // App should behave in line with expecations
        // set in Android SDK 27.
        targetSdkVersion 27
        // App version as integer
        versionCode 3
        versionName "0.5"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}
dependencies {
    // Download Xposed libraries and docs from Bintray/jCenter:
    // https://bintray.com/rovo89/de.robv.android.xposed/api
    provided 'de.robv.android.xposed:api:82'
    provided 'de.robv.android.xposed:api:82:sources'
    // support-v4 needed for ViewPager
    compile 'com.android.support:support-v4:27.0.2'
    // support-v13 needed for FragmentPagerAdapter to allow ViewPager to work with native Fragments
    compile 'com.android.support:support-v13:27.0.2'
    // Animated play/pause/stop button
    compile 'com.github.timrijckaert:FloatingMusicActionButton:v2.0.4'
    // Slider preference
    compile 'com.github.jayschwa:AndroidSliderPreference:v1.0.1'
    // Remote preference access
    compile 'com.crossbowffs.remotepreferences:remotepreferences:0.5'
    // EditFloatPreference and EditIntPreference
    compile 'com.mapzen.android:prefs-plus:1.0.0'

    // Other
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:27.0.2'
}

